# Pattern Rules

## How Snakemake determines the build order when multiple rules are present

Now that we have worked with multiple rules and seen how rules can call other rules, let us try to understand the principle behind this.

As we know, we can execute any rule in `Snakefile` explicitly by calling it by name like so:

```{r, engine = 'bash', eval = FALSE}
snakemake --cores 1 solow_intermediate
```

When no rule name is explicitly given, Snakemake will execute the first rule it encounters at the top of `Snakefile`:

```{r, engine = 'bash', eval = FALSE}
snakemake --cores 1
```

If you followed the order of rules in the solution to the previous exercise, `solow_intermediate` is the first rule in `Snakefile`.
In this case, command are equivalent.

The rule we ask `Snakemkake` to execute either explicitly or implicitly is called a **target rule**.

If all necessary inputs to build the target rule exist, Snakemake will execute the rule and build the defined outputs.

When Snakemake recognizes that a necessary `input` to execute the target rule is missing, Snakemake will try to build it through the other rules present in `Snakefile`.

To illustrate this, let us stick to our present project and assume the target rule is `solow_intermediate`.
The following graph shows all rules which lead up to the target rule:

![filegraph](img/filegraph.pdf)

`mrw_complete.csv` is a necessary input to `solow_intermediate`.
If the file does not exist, `Snakemake` will search through `Snakefile` for another rule which has `mrw_complete.csv` as its output -- in our case `gen_regression_vars`.
In this case, Snakemake will first execute `gen_regression_vars` before executing the target rule `solow_intermediate`.

If necessary inputs are missing to execute `gen_regression_vars`, Snakemake will search for other rules to produce it and so forth...

To allow Snakemake to do work properly, the input and output relationships in a project need to follow a **directed acyclic graph (DAG)**.
Directed here means that....
Acyclic implies that there can be no two rules which require output from one another as inputs, either directly or in a larger loop.

This has a few implications on how we should define our intermediate input and output files.
We should follow the following rules to prevent problems:

- **No rule should have use the same file as input and output.** It is often common practice to load a certain dataset, perform operations on it and overwrite the original file. While there are good reasons to never do this in any workflow, this behavior can lead to more severe problems when using Snakemake. Snakemake only seaches for rules which can create input files which do not yet exist. If a rule overwrites an already existing input file, Snakemake would not execute it as a dependency for a downstream target rule and the changes of such a rule would not be reflected in the outputs of the target rule. For this reason, `gen_regression_vars` writes its results to a new file in this project.
- **No two rules should have the same output.** When Snakemake searches for a file to create a necessary input, it will execute the first rule it encounters which produces the file. To make input and output relationships explicit and reproducible, we do not want to let this be determined by chance.
- **A project should have a clear direction.** The projects needs to be directed which implies that the execution of a project should have a clear trajetory. In practice this typically starts with data manipulations which are necessary to perform analysis, which in turn will be used to create plots and tables, which finally end up in a paper or set of slides.
Directedness here for example implies that the regression table cannot be an input to data manipulations etc.
- **A project cannot be circular.** Any circularity would let Snakemake search for rules in an infinite loop.


## Where we are now?

If you still have the `hello_world` rule in your Snakefile, now is a good moment to remove it.
Then, your Snakefile should look something like this:

```{r, engine = 'bash', eval = FALSE}
## Snakemake - MRW Replication
##
## @yourname


# --- Build Rules --- #

rule solow_intermediate:
    input:
        script   = "src/analysis/estimate_ols_model.R",
        data     = "out/data/mrw_complete.csv",
        model    = "src/model-specs/model_solow.json",
        subset   = "src/data-specs/subset_intermediate.json"
    output:
        estimate = "out/analysis/model_solow_subset_intermediate.rds",
    shell:
        "Rscript {input.script} \
            --data {input.data} \
            --model {input.model} \
            --subset {input.subset} \
            --out {output.estimate}"

rule solow_nonoil:
    input:
        script   = "src/analysis/estimate_ols_model.R",
        data     = "out/data/mrw_complete.csv",
        model    = "src/model-specs/model_solow.json",
        subset   = "src/data-specs/subset_nonoil.json"
    output:
        estimate = "out/analysis/model_solow_subset_nonoil.rds",
    shell:
        "Rscript {input.script} \
            --data {input.data} \
            --model {input.model} \
            --subset {input.subset} \
            --out {output.estimate}"

rule solow_oecd:
    input:
        script   = "src/analysis/estimate_ols_model.R",
        data     = "out/data/mrw_complete.csv",
        model    = "src/model-specs/model_solow.json",
        subset   = "src/data-specs/subset_oecd.json"
    output:
        estimate = "out/analysis/model_solow_subset_oecd.rds",
    shell:
        "Rscript {input.script} \
            --data {input.data} \
            --model {input.model} \
            --subset {input.subset} \
            --out {output.estimate}"

rule gen_regression_vars:
    input:
        script = "src/data-management/gen_reg_vars.R",
        data   = "out/data/mrw_renamed.csv"
    output:
        data   = "out/data/mrw_complete.csv"
    shell:
        "Rscript {input.script} \
            --data {input.data} \
            --out {output.data}"

rule rename_vars:
    input:
        script = "src/data-management/rename_variables.R",
        data   = "src/data/mrw.dta"
    output:
        data = "out/data/mrw_renamed.csv"
    shell:
        "Rscript {input.script} \
            --data {input.data} \
            --out {output.data}"

# --- Clean Rules --- #

rule clean:
    shell:
        "rm -rf out/*"

rule clean_data:
    shell:
        "rm -rf out/data/*"

rule clean_analysis:
    shell:
        "rm -rf out/analysis/*"

```


This is good progress, but if we look at the *solow_* rules we see that there is quite a lot of duplication.
All rules three use the same script, data, and model inputs.
They only differ in the data subsetting found under `{input.subset}` and the output file define under `{output.estimate}`.

Ideally, we want `Snakefile` to feature the minimum amount of duplication possible.
This has a few advantages:

Limiting redundancy allows us to make changes to the code only once rather than multiple times (imagine changing the `{data.input}` file).
Any additional copy of code also introduces extra opportunities of typos or other errors and makes the build file generally harder to read and navigate.
And if we're completely honest, it's also a just a bit ugly...

In this chapter we will 


A straight forward solution to remove redundancy is 
The three *solow_* rules can be collapsed into one rule if can create a variable, for example `iSubset`,
    that can iterate through the three .json files that contain the subset filters.
That is, we want to create a rule `solow_model` that can do the work that `solow_nonoil`, `solow_oecd` and `solow_intermediate` currently do.
In Snakemake, these variables are called *wildcards*.

The format of this rule will be:

```{r, engine = 'bash', eval = FALSE}
rule solow_model:
    input:
        script = "src/analysis/estimate_ols_model.R",
        data   = "out/data/mrw_complete.csv",
        model  = "src/model-specs/model_solow.json",
        subset = "src/data-specs/subset_{iSubset}.json"
    output:
        model_est = "out/analysis/model_solow_{iSubset}.rds",
    shell:
        "Rscript {input.script} \
            --data {input.data} \
            --model {input.model} \
            --subset {input.subset} \
            --out {output.model_est}"
```

We wrapped our wildcard `iSubset` in curly parentheses so that Snakemake knows that we will want to substitute the name of one of the subsets into this value.
This is conceptually similar to what we have done in our shell commands.

We can now try and run our updated Snakefile:

```{r, engine = 'bash', eval= FALSE}
$ snakemake
```

```{r, engine = 'out', eval = FALSE}
Building DAG of jobs...
WorkflowError:
Target rules may not contain wildcards. Please specify concrete files or a rule without wildcards.
```

What has happened?
Snakemake will not execute a rule that contains wildcards.^[
Technically there is another problem here too.
Snakemake doesn't know what values to substitute into `iSubset.`
We focus on the Wildcard error because this is what the Snakemake error message mentions.
By fixing this error, it turns out we also spell out what values to substitute into `iSubset`.
]
What we will do is create another rule, `run_solow` that will not contain wildcards ...

```{r, engine = 'bash', eval = FALSE}

rule run_solow:
    input:
        nonoil = "out/analysis/model_solow_nonoil.rds",
        oecd   = "out/analysis/model_solow_oecd.rds",
        intermediate = "out/analysis/model_solow_intermediate.rds"

rule solow_model:
    input:
        script = "src/analysis/estimate_ols_model.R",
        data   = "out/data/mrw_complete.csv",
        model  = "src/model-specs/model_solow.json",
        subset = "src/data-specs/subset_{iSubset}.json"
    output:
        model_est = "out/analysis/model_solow_{iSubset}.rds",
    shell:
        "Rscript {input.script} \
            --data {input.data} \
            --model {input.model} \
            --subset {input.subset} \
            --out {output.model_est}"
```

Explain what happens here...

There's more work we can do to reduce duplication.
Look at the rule `run_solow`:

```{r, engine = 'bash', eval = FALSE}
rule run_solow:
    input:
        nonoil = "out/analysis/model_solow_nonoil.rds",
        oecd   = "out/analysis/model_solow_oecd.rds",
        intermediate = "out/analysis/model_solow_intermediate.rds"
```

## The `expand()` function

Each of these inputs listed above have similar structure, with only the name of the subset of data we are using changing.
We can use another feature of Snakemake to simplify this rule.
Snakemake has an `expand()` function that can accept a wildcard and replace it with a set of specified values in an iterative manner.
In our case, we want to use the expand function to accept the `{iSubset}` wildcard, and replace it with the values 'nonoil', 'oecd' and 'intermediate' one at a time.
To proceed we need to do two things:

1. Create a list, DATA_SUBSETS, that contains the values we want to iterate through - 'nonoil', 'oecd' and 'intermediate'.
2. Use Snakemake's `expand()` function to iteratively replace `{iSubset}` with each value contained in the list `DATA_SUBSET`

Let's start with (1).
We will use an area above our Snakemake rules to store the `DATA_SUBSET` variable:

```{r, engine = 'bash', eval = FALSE}
DATA_SUBSET = [
                "oecd",
                "intermediate",
                "nonoil"
                ]

# --- Build Rules --- #
...
```

The capitalization of the list DATA_SUBSET is not essential.
We do it to separate lists that we will iterate through from other parts of our Snakefile.
This means whenever we see a capitalized name, we know it is a list that we want to iterate through.

Next, we update the rule `run_solow` as follows:

```{r, engine = 'bash', eval = FALSE}
rule run_solow:
    input:
        expand("out/analysis/model_solow_{iSubset}.rds",
                    iSubset = DATA_SUBSET)
```

Now, we can clean our output folder with `$ snakemake clean`.
If we re-run snakemake we would expect everything to run.
To see if this is the case we can do a *dry run*.
A dry run will try go through the Snakefile and print all the rules Snakemake wants to execute in order.

```{r, engine = 'bash', eval = FALSE}
$ snakemake --dryrun
```

which yields the following plan:

```{r, engine = 'out', eval = FALSE}
Building DAG of jobs...
Job counts:
	count	jobs
	1	gen_regression_vars
	1	rename_vars
	1	run_solow
	3	solow_model
	6

[Fri Jan 11 17:00:32 2019]
rule rename_vars:
    input: src/data/mrw.dta, src/data-management/rename_variables.R
    output: out/data/mrw_renamed.csv
    jobid: 5


[Fri Jan 11 17:00:32 2019]
rule gen_regression_vars:
    input: out/data/mrw_renamed.csv, src/data-management/gen_reg_vars.R, src/data-specs/param_solow.json
    output: out/data/mrw_complete.csv
    jobid: 4


[Fri Jan 11 17:00:32 2019]
rule solow_model:
    input: src/data-specs/subset_nonoil.json, out/data/mrw_complete.csv, src/model-specs/model_solow.json, src/analysis/estimate_ols_model.R
    output: out/analysis/model_solow_nonoil.rds
    jobid: 1
    wildcards: iSubset=nonoil


[Fri Jan 11 17:00:32 2019]
rule solow_model:
    input: src/data-specs/subset_oecd.json, out/data/mrw_complete.csv, src/model-specs/model_solow.json, src/analysis/estimate_ols_model.R
    output: out/analysis/model_solow_oecd.rds
    jobid: 2
    wildcards: iSubset=oecd


[Fri Jan 11 17:00:32 2019]
rule solow_model:
    input: src/data-specs/subset_intermediate.json, out/data/mrw_complete.csv, src/model-specs/model_solow.json, src/analysis/estimate_ols_model.R
    output: out/analysis/model_solow_intermediate.rds
    jobid: 3
    wildcards: iSubset=intermediate


[Fri Jan 11 17:00:32 2019]
localrule run_solow:
    input: out/analysis/model_solow_nonoil.rds, out/analysis/model_solow_oecd.rds, out/analysis/model_solow_intermediate.rds
    jobid: 0

Job counts:
	count	jobs
	1	gen_regression_vars
	1	rename_vars
	1	run_solow
	3	solow_model
	6

```

This looks like what we want to happen.
Hence, we re-run snakemake to produce all output:

```{r, engine = 'bash', eval = FALSE}
$ snakemake
```

### Exercise: Exploring the expand() function I {- .exercise}
So far we have estimated the basic Solow model.
MRW also estimate an augmented version of the Solow model, adding human capital.
The formula required to estimate the augmented model is written up in `src/model-specs/model_aug_solow.json`.
Use the `expand()` function together with the `estimate_ols.R` script to estimate the augmented solow model on each of the three data subsets.
The rule structures should look very similar to what we have done so far.

### Exercise: Exploring the expand function II {- .exercise}
The MRW paper contains three plots.
Each of these plots use the subset of 'intermediate' countries.
In the `src/figures/` subdirectory, there are three scripts that reproduce each of the figures.^[
This is not entirely true, we are yet to figure out how to get the y-axis range from the original paper.
]
The scripts are written in such a way that they accept exactly the same options.
Using wildcards and the expand function extend the Snakefile to construct each figure.
Each figure should be saved with the following name 'out/figures/SCRIPTNAME.pdf'

## Expanding Multiple Wildcards

The rules used to estimate the standard Solow model, and the augmented Solow model have very similar structure:

```{r, engine = 'bash', eval = FALSE}
DATA_SUBSET = [
                "oecd",
                "intermediate",
                "nonoil"
                ]

# --- Build Rules --- #
rule run_aug_solow:
    input:
        expand("out/analysis/model_aug_solow_{iSubset}.rds",
                    iSubset = DATA_SUBSET)

rule aug_solow_model:
    input:
        script = "src/analysis/estimate_ols_model.R",
        data   = "out/data/mrw_complete.csv",
        model  = "src/model-specs/model_aug_solow.json",
        subset = "src/data-specs/subset_{iSubset}.json"
    output:
        model_est = "out/analysis/model_aug_solow_{iSubset}.rds",
    shell:
        "Rscript {input.script} \
            --data {input.data} \
            --model {input.model} \
            --subset {input.subset} \
            --out {output.model_est}"

rule run_solow:
    input:
        expand("out/analysis/model_solow_{iSubset}.rds",
                    iSubset = DATA_SUBSET)

rule solow_model:
    input:
        script = "src/analysis/estimate_ols_model.R",
        data   = "out/data/mrw_complete.csv",
        model  = "src/model-specs/model_solow.json",
        subset = "src/data-specs/subset_{iSubset}.json"
    output:
        model_est = "out/analysis/model_solow_{iSubset}.rds",
    shell:
        "Rscript {input.script} \
            --data {input.data} \
            --model {input.model} \
            --subset {input.subset} \
            --out {output.model_est}"
```

Add text ...

Ultimately the Snakfile becomes:

```{r, engine = 'bash', eval = FALSE}
MODELS = [
          "solow",
          "aug_solow"
          ]

DATA_SUBSET = [
                "oecd",
                "intermediate",
                "nonoil"
                ]

# --- Build Rules --- #

rule estimate_models:
    input:
        expand("out/analysis/{iModel}_{iSubset}.rds",
                    iModel = MODELS,
                    iSubset = DATA_SUBSET)

rule ols_model:
    input:
        script = "src/analysis/estimate_ols_model.R",
        data   = "out/data/mrw_complete.csv",
        model  = "src/model-specs/model_{iModel}.json",
        subset = "src/data-specs/subset_{iSubset}.json"
    output:
        model_est = "out/analysis/model_{iModel}_{iSubset}.rds",
    shell:
        "Rscript {input.script} \
            --data {input.data} \
            --model {input.model} \
            --subset {input.subset} \
            --out {output.model_est}"

<...> ## Other Rules below
```
