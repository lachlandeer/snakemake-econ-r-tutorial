# PART I - Basic Snakemake Workflows {-}

# Initial Steps with Snakemake

Learning goals

- Where and how do we write Snakemake rules?
- How do we execute a rule?
- How can rules interact with input and output files?
- How does Snakemake deal with redundant executions?


Now that we know the structure of our project, we are ready to start building a fully reproducible pipeline.
Snakemake expects instructions in a file called `Snakefile` in the current folder.
A `Snakefile` is a collection of rules that together define the order in which a project will be executed.

We have added an empty `Snakefile` in the main project folder.
You can edit this file in a text editor of your choice.
In the remainder of this tutorial we will fill the file together with a pipeline which reproduces the results from @mrw.

## Snakefile -- Writing Our First Rule

By law any software tutorial is required to start with a hello world example, so we'll follow court and do the same.
To do so, we open `Snakefile` in a text editor of our choice and write the following text into the file:

```{r, engine = 'bash', eval = FALSE}
rule hello_world:
  shell:
    'echo "Hello World!"'
```

Snakemake instructions are written in Python 3 with a few extra objects and methods provided by Snakemake.
The structure of our rule will look very familiar to anybody who has written Python functions before.
This implies that indentation is interpreted by the language and that divinations from correct indentation will result in errors.
The first line in our example defines the name of a rule, in this case `hello_world`.
The part following `shell`: tells Snakemake that this rule executes a shell command, which in our case prints `Hello World!` to the console.

## The Snakemake Command -- Executing a Rule 

After saving our changes to `Snakefile`, we can execute the newly created rule by navigating to the project's main folder and by typing `Snakemake --cores 1`.
By default Snakemake will execute the first rule it encounters in a `Snakefile`.
As our file only contains a single rule, it will execute the `hello_world` rule and print `Hello World!` to the console together with some additional information about the execution of the work flow.
When a file contains more than a single rule, we often want to be more explicit and tell `Snakemake` to execute a particular rule like so:

```{r, engine = 'bash', eval = FALSE}
$ snakemake --cores 1 name_of_rule
```

In our case `snakemake --cores 1 hello_world` will give the same output as the last execution.

At this point you are probably correctly guessing that the long option `--cores #n` tell Snakemake how many CPU cores to use when executing a workflow. 
Running workflows in parallel can speed up complex workflows substantially.
In this guide we will execute all workflows with a single core to make the code compatible with all computers but you are encouraged to choose a core count appropriate to your system instead.

## Reading and Writing Files -- First Steps in a Data Science Pipeline  

Like most research, our project starts with data management (also see Chapter XX).
The data management subdirectory, `src/data-management` contains two scripts

```{r, engine = 'out', eval = FALSE}
rename_variables.R
gen_reg_vars.R
```

which need to be executed sequentially.
In this chapter, we want to run the script `rename_variables.R` to tidy up the variable names in our data set.
In the next chapter, we will then add `gen_reg_vars.R`, which creates additional variables that will be needed to run some regressions in later steps, and learn how to execute both rules sequentially to in a first, minimum pipeline. 

### Using the Rscript command to execute R scripts

We can run any R script with the `Rscript` shell command.
The command expects the path to an `.R` script as the first argument.
Simple R scripts that do not require additional options can be just run as such:

```{r, engine = 'bash', eval = FALSE}
Rscript PATH/TO/SCRIPT.R
```

Most scripts in this project have been written to require additional parameters.
We can run any R script that requires parameters with the `--help` option to see a list of arguments.

Let us apply this and execute the `rename_variables.R` script with the `--help` flag:

```{r, engine = 'bash', eval = FALSE}
$ Rscript src/data-management/rename_variables.R --help
```

This prints the following output to the console:

```{r, engine = 'bash', eval = FALSE}
Usage: src/data-management/rename_variables.R [options]

Options:
	-d CHARACTER, --data=CHARACTER
		stata dataset file name

	-o CHARACTER, --out=CHARACTER
		output file name [default = out.csv]

	-h, --help
		Show this help message and exit
```

This suggests the script expects

1. a `--data` option which defines the filepath to a stata data set. This is the input dataset which the script transforms.
2. a `--out` option which defines the filepath the transformed data set will be saved to.

Next we will learn how to define inputs and outputs in snakemake.

### The input and output arguments

Our `hello_world` rule was very simple and did not interact with the file system.
In a research workflow we typically want to interact with **input** files such as data or R scripts which produce one or more **outputs** like transformed data, tables, or graphs.
A snakemake rule can be thought of as the recipe that combines  the inputs to produce the outputs. 

`Snakemake` expects these components to be provided in a particular way so that it knows what to do with the information we provide.
We are going to specify rules in the following format:

```{r, engine = 'bash', eval = FALSE}
rule rule_name:
    input:
        input_name1 = "PATH/TO/input_one",
        input_name2 = "PATH/TO/input_two"
    output:
        output_name1 = "PATH/TO/SAVE/output_one",
        output_name2 = "PATH/TO/SAVE/output_two"
    shell:
        "HOW TO MIX IT ALL TOGETHER"
```

We can have as many inputs and outputs as the rule needs to function.
Each input and each output are given names, for example `input_name1` which take the value to the file path and name of the file.
It is important to wrap each of these paths into quotations, and to separate each of the multiple inputs and outputs with a comma.

### A rule to rename variables.

Let us now define a snakemake rule which executes the `rename_variables.R` script with the correct input and output paths.
To do so, we copy the example rule into our snakefile above the `hello_world` rule.
Let us also rename the rule to `rename_vars`.

The rule will use the `rename_variables.R` script as a first input.
Let us therefore rename the first input into `script` and replace the right hand side with the path to the `rename_variables.R` script which we have used above when we printed the script's help function.

Our `rename_vars` rule should now look something like this:

```{r, engine = 'bash', eval = FALSE}
rule rename_vars:
    input:
        script = "src/data-management/rename_variables.R",
        input_name2 = "PATH/TO/input_two"
    output:
        output_name1 = "PATH/TO/SAVE/output_one",
        output_name2 = "PATH/TO/SAVE/output_two"
    shell:
        "HOW TO MIX IT ALL TOGETHER"
```

Next, we want to add any additional inputs and also specify any outputs that the file produces.
From the `help` output we know the script expects a stata dataset as an additional input which we can find in the `src/data/` folder.
We rename the second input into `data` and replace the right hand side with the path to @mrw's stata dataset.
Finally, we replace the first output name with `data` and define an appropriate output file path as the right hand side.
This is the location our transformed data will be saved to.

After these steps, our rule should look something like this:

```{r, engine = 'bash', eval = FALSE}
rule rename_vars:
    input:
        script = "src/data-management/rename_variables.R",
        data   = "src/data/mrw.dta"
    output:
        data = "out/data/mrw_renamed.csv"
    shell:
        "HOW TO MIX IT ALL TOGETHER"
```

Lastly, we need to edit the `shell` command to combine inputs and output.
We know that we can execute the R script via the `Rscript` command with the path to the script as the first argument.
Gladly, snakemake does not require it to write the full path out again.
We can instead refer to the input's and output's which we have defined via their names which Snakemake expects within `{}`.
Executing an R script can therefore be done via `Rscript {input.script}`.
The dataset and the output location can be passed to our script as `--data` and `--out` arguments and we will pass it the input and output options which we have defined in our rule. 

Putting it all together will give us the following rule:

```{r, engine = 'bash', eval = FALSE}
rule rename_vars:
    input:
        script = "src/data-management/rename_variables.R",
        data   = "src/data/mrw.dta"
    output:
        data = "out/data/mrw_renamed.csv"
    shell:
        "Rscript {input.script} \
            --data {input.data} \
            --out {output.data}"
```

Note that `\` in the shell command allows us to write the command over several lines for better readability. 
They can be omitted when we write the command in a single line.

After saving we can execute our newly rule via:

```{r rename-vars-6, engine = 'bash', eval = FALSE}
$ snakemake --cores 1 rename_vars
```

Snakemake will give us some console output informing us about the execution of our rule. 
A successful execution will be displayed through green text ending in 

```{r, engine = 'bash', eval = FALSE}
TODO: Make green
Finished job 0.
1 of 1 steps (100%) done
```

If the execution was successful, we can look into our output directory to see if anything has happened:

```{r, engine = 'bash', eval = FALSE}
$ ls out/data/
```

which yields

```{r, engine = 'out', eval=FALSE}
mrw_renamed.csv
```

Our file has been created as we expected.

## Redundant Executions -- (Don't) Run a Rule Repeatedly

What would happen if the run the rule a second time after it has been successfully completed before?
Let us execute the `rename_vars` rule a second time:

```{r, engine = 'bash', eval = FALSE}
$ snakemake --cores 1 rename_vars
```

We see the following output:

```{r, engine = 'out', eval = FALSE}
Building DAG of jobs...
Nothing to be done.
```

To understand the status of the project as seen by Snakemake, we can use the **summary** option as such:

```{r, engine = 'bash', eval = FALSE}
$ snakemake --summary
```

In our case the output following output is printed to the console:

```{r, engine = 'out', eval = FALSE}
Building DAG of jobs...
output_file	                      date	                  rule	    version	 log-file(s)	status	plan
out/data/mrw_renamed.csv	Thu Jan 10 20:31:08 2019	rename_vars	   -		             ok	    no update

```

Snakemake tells us the status of the output file of the `rename_vars` rule is `ok` and that it plans not to update the file.

Now, let us what happens if we update one of the input files.
The following command updates the time the `rename_variables.R` was last edited without changing the actual content of the file: 

```{r, engine = 'bash', eval = FALSE}
$ touch src/data-management/rename_variables.R
```

Running the summary command again

```{r, engine = 'bash', eval = FALSE}
$ snakemake --summary
```

yields the following output:

```{r, engine = 'out', eval = FALSE}
Building DAG of jobs...
output_file	                       date	                   rule	    version	log-file(s)	     status	               plan
out/data/mrw_renamed.csv	Thu Jan 10 20:31:08 2019	rename_vars	   -		        updated input files	update pending
```

Snakemake has detected that one input file is more recent than the output file.
It therefore will execute the rule when asked to do so.

Running the rule again via

```{r, engine = 'bash', eval = FALSE}
snakemake --cores 1 rename_vars
```

will execute the rule.

We see one mayor advantage of Snakemake over simpler and very common alternatives to execute projects.
Often researchers chain do files together in a linear and static way.
It is very common in such a workflow to comment out chunks of the code to prevent redundant executions to safe time.

Snakemake keeps track of dependencies and updates to input files.
We therefore do not have to worry about preventing the execution of previously run scripts to gain efficiency and safe time.
We can simply run the most down stream rule of our project and let Snakemake figure out which upstream rules need to be re-run.
We will explain later in this tutorial how to write an `all` rule which takes care of this.